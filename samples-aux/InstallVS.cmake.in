set(PROJECT_SOURCE_DIR "@PROJECT_SOURCE_DIR@")
set(SAMPLES_SOURCE_DIR "@SAMPLES_SOURCE_DIR@")
set(PROJECT_BINARY_DIR "@PROJECT_BINARY_DIR@")
set(SAMPLES_BUILD_TEMP_DIR "@SAMPLES_BUILD_TEMP_DIR@")
set(CMAKE_GENERATOR "@CMAKE_GENERATOR@")
set(CMAKE_GENERATOR_PLATFORM @CMAKE_GENERATOR_PLATFORM@)
set(CLISP_EXECUTABLE "@CLISP_EXECUTABLE@")
set(CMAKE_COMMAND "@CMAKE_COMMAND@")
set(ASTRA_64 @ASTRA_64@)
set(ASTRA_32 @ASTRA_32@)
set(VS_DIR @VS_DIR@)

MESSAGE("Configuring sdk sample ${VS_DIR} projects...")

file(REMOVE_RECURSE ${SAMPLES_BUILD_TEMP_DIR})

if (ASTRA_64)
  set(SFML_LIB_DIR "SFML-min-64")
  set(CMAKECACHE_IN "CMakeCache.txt.${VS_DIR}.x64.in")
  set(ARCH "-A${CMAKE_GENERATOR_PLATFORM}")
else()
  set(SFML_LIB_DIR "SFML-min-32")
  set(CMAKECACHE_IN "CMakeCache.txt.${VS_DIR}.x86.in")
  set(ARCH "")
endif()

configure_file("${PROJECT_SOURCE_DIR}/samples-aux/${CMAKECACHE_IN}"
  "${SAMPLES_BUILD_TEMP_DIR}/CMakeCache.txt"
  @ONLY)

execute_process(
  COMMAND ${CMAKE_COMMAND}
  -G${CMAKE_GENERATOR} ${ARCH} "${SAMPLES_BUILD_TEMP_DIR}"
  WORKING_DIRECTORY
  ${SAMPLES_SOURCE_DIR})

MESSAGE("Deleting CMake generated directories and files...")

set(CMAKE_GEN_FILES_TO_DELETE
  "${SAMPLES_BUILD_TEMP_DIR}/ALL_BUILD.vcxproj"
  "${SAMPLES_BUILD_TEMP_DIR}/ALL_BUILD.vcxproj.filters"
  "${SAMPLES_BUILD_TEMP_DIR}/CMakeCache.txt"
  "${SAMPLES_BUILD_TEMP_DIR}/ZERO_CHECK.vcxproj"
  "${SAMPLES_BUILD_TEMP_DIR}/ZERO_CHECK.vcxproj.filters"
  )

file(GLOB_RECURSE FOUND_FILES_TO_DELETE "${SAMPLES_BUILD_TEMP_DIR}/cmake_install.cmake"
                                        "${SAMPLES_BUILD_TEMP_DIR}/INSTALL.vcxproj"
                                        "${SAMPLES_BUILD_TEMP_DIR}/INSTALL.vcxproj.filters")

FILE(GLOB_RECURSE cmake_stamp_list "${SAMPLES_BUILD_TEMP_DIR}/generate.stamp")
SET(FOUND_DIRS_TO_DELETE "")
FOREACH(file_path ${cmake_stamp_list})
  GET_FILENAME_COMPONENT(dir_path ${file_path} DIRECTORY)
  LIST(APPEND FOUND_DIRS_TO_DELETE "${dir_path}")
ENDFOREACH()
LIST(REMOVE_DUPLICATES FOUND_DIRS_TO_DELETE)

MESSAGE("Removing directories: ${FOUND_DIRS_TO_DELETE}")

file(REMOVE ${CMAKE_GEN_FILES_TO_DELETE})

if(NOT "${FOUND_FILES_TO_DELETE}" STREQUAL "")
  file(REMOVE ${FOUND_FILES_TO_DELETE})
endif()

if(NOT "${FOUND_DIRS_TO_DELETE}" STREQUAL "")
  file(REMOVE_RECURSE ${FOUND_DIRS_TO_DELETE})
endif()

MESSAGE("Removing CMake from ${VS_DIR} projects...")

execute_process(COMMAND ${CLISP_EXECUTABLE} "parse-replace.cl" "${SAMPLES_BUILD_TEMP_DIR}" "${SAMPLES_SOURCE_DIR}"
  WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}/lpp/")

MESSAGE("Done configuring sdk sample ${VS_DIR} projects.")
